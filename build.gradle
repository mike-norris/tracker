plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'idea'
}

group = 'com.openrangelabs.tracer'
version = '1.0.0'
description = 'Distributed tracing and monitoring starter for Spring Boot applications'

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.4.0'
    }
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter'
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-data-jdbc'
    api 'org.springframework.boot:spring-boot-starter-aop'

    implementation 'org.postgresql:postgresql:42.7.7'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'

    compileOnly 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Configure JAR for library distribution
jar {
    archiveBaseName = 'tracer'
    archiveVersion = '1.0'
    enabled = true
}

// Publishing configuration
publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'Tracer'
                description = 'Distributed tracing and monitoring starter for Spring Boot applications'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/mike-norris/tracker/refs/heads/main/LICENSE'
                    }
                }
            }
        }
    }
}